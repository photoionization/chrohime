import("//tools/grit/repack.gni")
import("//tools/v8_context_snapshot/v8_context_snapshot.gni")

component("content") {
  sources = [
    "chrohime_content_browser_client.cc",
    "chrohime_content_browser_client.h",
    "chrohime_content_client.cc",
    "chrohime_content_client.h",
    "chrohime_content_main_delegate.cc",
    "chrohime_content_main_delegate.h",
    "chrohime_content_main_main_parts.cc",
    "chrohime_content_main_main_parts.h",
    "content_lifetime_delegate.h",
  ]
  if (is_apple) {
    sources += [ "chrohime_content_main_delegate_mac.mm" ]
  }

  configs += [ "//chrohime:chrohime_internal_config" ]

  # Expose all public content APIs.
  public_deps = [
    "//content/public/app",
    "//content/public/browser",
    "//content/public/child",
    "//content/public/common",
    "//content/public/gpu",
    "//content/public/renderer",
    "//content/public/utility",
  ]

  deps = [
    ":pak",
    "//content",
  ]
  if (v8_use_external_startup_data) {
    if (use_v8_context_snapshot) {
      deps += [ "//tools/v8_context_snapshot" ]
    } else {
      deps += [ "//v8" ]
    }
  }
}

repack("pak") {
  output = "$root_out_dir/chrohime_with_content.pak"
  sources = [
    "$root_out_dir/chrohime.pak",
    "$root_gen_dir/third_party/blink/public/resources/blink_resources.pak",
    "$root_gen_dir/third_party/blink/public/resources/blink_scaled_resources_100_percent.pak",
    "$root_gen_dir/third_party/blink/public/resources/inspector_overlay_resources.pak",
    "$root_gen_dir/third_party/blink/public/strings/blink_accessibility_strings_en-US.pak",
    "$root_gen_dir/third_party/blink/public/strings/blink_strings_en-US.pak",
  ]
  deps = [
    "//chrohime:chrohime_pak",
    "//third_party/blink/public:devtools_inspector_resources",
    "//third_party/blink/public:resources",
    "//third_party/blink/public:scaled_resources_100_percent",
    "//third_party/blink/public/strings",
    "//third_party/blink/public/strings:accessibility_strings",
  ]
}
