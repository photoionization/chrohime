import("//build/config/ui.gni")
import("//tools/grit/repack.gni")

component("content") {
  # TODO(zcbenz): views:test_support is a testonly dependency.
  testonly = true

  sources = [
    "chrohime_content_browser_client.cc",
    "chrohime_content_browser_client.h",
    "chrohime_content_client.cc",
    "chrohime_content_client.h",
    "chrohime_content_main_delegate.cc",
    "chrohime_content_main_delegate.h",
    "chrohime_content_main_main_parts.cc",
    "chrohime_content_main_main_parts.h",
    "content_lifetime_delegate.h",
  ]
  if (is_apple) {
    sources += [ "chrohime_content_main_delegate_mac.mm" ]
  }

  configs += [ "//chrohime:chrohime_internal_config" ]

  deps = [
    ":pak",
    "//content",
    "//ui/views:test_support",
  ]
  if (is_linux) {
    deps += [ "//ui/linux:linux_ui_factory" ]
  }
  if (use_aura) {
    deps += [
      "//ui/aura",
      "//ui/wm",
    ]
  }
}

repack("pak") {
  # TODO(zcbenz): content/shell is a testonly dependency.
  testonly = true

  output = "$root_out_dir/chrohime_with_content.pak"
  sources = [
    "$root_out_dir/content_shell.pak",
  ]
  deps = [
    "//content/shell:pak",
  ]
}
