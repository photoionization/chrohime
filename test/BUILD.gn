executable("chrohime_test") {
  testonly = true
  sources = [ "main.cc" ]

  if (is_mac) {
    deps = [ "//chrohime:chrohime_with_content_with_lifetime" ]
  } else {
    deps = [ "//chrohime:chrohime_no_content_with_lifetime" ]
  }

  if (is_win) {
    deps += [ "//build/win:default_exe_manifest" ]
    configs += [ "//build/config/win:windowed" ]
    configs -= [ "//build/config/win:console" ]
  }
}

executable("chrohime_gallery") {
  testonly = true
  sources = [ "gallery.c" ]
  deps = [ "//chrohime/c:chrohime_shared_c_lib" ]

  if (is_win) {
    deps += [ "//build/win:default_exe_manifest" ]
  }

  if (!is_component_build) {
    # Set rpath to search libchrohime from out dir.
    if (is_linux) {
      configs += [ "//build/config/gcc:rpath_for_built_shared_libraries" ]
    } else if (is_mac) {
      ldflags = [ "-Wcrl,installnametool,-change,./libchrohime_shared_c_lib.dylib,@executable_path/libchrohime_shared_c_lib.dylib" ]
    }
  }
}
