executable("chrohime_gallery") {
  sources = [ "gallery.c" ]

  deps = [ "//chrohime/dylib:chrohime_with_content_dylib" ]
  if (is_win) {
    # Without a proper manifest file, GPU process will crash at CreateWindowEx
    # in child_window_win.cc.
    deps += [ "//build/win:default_exe_manifest" ]
  }

  # Clang reports raw_ptr warning in C header.
  configs -= [ "//build/config/clang:find_bad_constructs" ]

  if (!is_component_build) {
    # Set rpath to search libchrohime from out dir.
    if (is_linux) {
      configs += [ "//build/config/gcc:rpath_for_built_shared_libraries" ]
    } else if (is_mac) {
      ldflags = [ "-Wcrl,installnametool,-change,./libchrohime_with_content.dylib,@executable_path/libchrohime_with_content.dylib" ]
    }
  }
}

# Copy the example code to root_out_dir which will be shipped in distribution.
copy("chrohime_copy_gallery") {
  sources = [
    "CMakeLists.txt",
    "gallery.c",
  ]
  if (is_win) {
    sources += [ "gallery.manifest" ]
  }
  outputs = [ "$root_out_dir/{{source_file_part}}" ]
}
